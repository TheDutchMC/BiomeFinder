import org.apache.tools.ant.filters.ReplaceTokens
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id 'eclipse'
    id 'com.github.johnrengelman.shadow' version '7.0.0'    
}

group 'dev.array21.worldfinder'

sourceCompatibility = 1.16
targetCompatibility = 1.16

repositories {
	mavenCentral()
	maven{ url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
	maven{ url "https://oss.sonatype.org/content/repositories/snapshots" }
}

processResources {
	from(sourceSets.main.resources.srcDirs) {
		filter ReplaceTokens, tokens: [version: pluginVersion]
		filter ReplaceTokens, tokens: [apiVersion: pluginApiVersion]
	}
}

dependencies {	
	compileOnly 'org.spigotmc:spigot-api:1.16.4-R0.1-SNAPSHOT'
	implementation 'dev.array21:bukkit-reflection-util:1.0'
	implementation group: 'commons-io', name: 'commons-io', version: '2.8.0'
}

processResources  {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

jar {
	archiveVersion.set(pluginVersion)
}

task testJar(type: ShadowJar) {
	description 'Build a test Jar'
	archiveClassifier = 'DEV'
	archiveVersion.set(pluginVersion)
	from sourceSets.main.output
	configurations = [project.configurations.runtimeClasspath]	
	destinationDirectory = file("$rootDir/server/plugins")
}

task releaseJar(type: ShadowJar) {
	description 'Build a release Jar'
	archiveClassifier = 'RELEASE'
	archiveVersion.set(pluginVersion)
	from sourceSets.main.output
	configurations = [project.configurations.runtimeClasspath]	
	destinationDirectory = file("$rootDir/releases")
}

//This task is for GitHub Actions only
task ghActions(type: ShadowJar) {	
	description 'GitHub Actions Task. Do not use in regular development!'
	archiveVersion.set('')
	archiveClassifier = ''
	archiveBaseName = 'output'
	
	from sourceSets.main.output
	configurations = [project.configurations.runtimeClasspath]	
	destinationDirectory = file("$rootDir/actions")
}

